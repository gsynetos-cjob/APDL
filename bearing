!-------------------------------------
! M A I N    B E A R I N G    L O A D
!-------------------------------------

/NERR,5,99999999,,0,0 ! Because of the issue turn off the warnings
!-----------
!USER INPUT
!------------
bearing_force = 105 	!Magnitude of bearing force
alpha = 120 		!Spreading angle of the contact area (120degrees is reasonable)
beta =  90  		!The angle to rotate the nodal coordinate system. It is required for the application of the Nodal Force (see NMODIF command)
!----------

LOCAL,1000,1,182.45,0,0,beta,,,,, !Create a Local Cylindrical System
*AFUN,Deg	!Calculation of angles is with Degrees (it is used in the cos(phi) fuction

cmsel,s,MB,NODE	!Select Nodal Group previously defined by a Name Selection

*get,nrNode,NODE,,count	!Returns the total Amount of Nodes in the selection
*dim,Results,array,nrNode,8		!Create an Array size (nrNode x 6)
*vget,Results(1,1),NODE,,NLIST	!Write the Node numbers in the first column

sum_cos = 0
*do,ii,1,nrNode
  *get,Results(ii,2),node,results(ii,1),LOC,X	!Write the X-coordinate in the 2nd column
  *get,Results(ii,3),node,results(ii,1),LOC,Y
  *get,Results(ii,4),node,results(ii,1),LOC,Z
  
   *if,Results(ii,3),ABLT,0.5*alpha,THEN		!If statement that verifies that which node belongs to the region that we the load is to be applied
   sum_cos = sum_cos + cos(Results(ii,3))		!Calculate the sum of the cosinus. It is used in the next loop to divide with the total Load 
   *endif
*enddo

*do,ii,1,nrNode
   *if,Results(ii,3),ABLT,0.5*alpha,THEN
	Results(ii,6)= cos(Results(ii,3))/sum_cos		!Calculate per each node the weight factor
	Results(ii,7)=bearing_force*Results(ii,6)*cos(beta)+bearing_force*Results(ii,6)*tan(Results(ii,3))*cos(90+beta)
	Results(ii,8)=bearing_force*Results(ii,6)*sin(beta)+bearing_force*Results(ii,6)*tan(Results(ii,3))*sin(90+beta)
	F,results(ii,1),FX,Results(ii,7)
	F,results(ii,1),FY,Results(ii,8)
   *endif

*enddo

Write data to file
*cfopen,'nodes','txt',' '
*vwrite,Results(1,1),Results(1,2),Results(1,3),Results(1,4),Results(1,5),Results(1,6),Results(1,7),Results(1,8)
(F10.0, F20.3 , F20.3, F20.3, F20.3, F20.3, F20.3, F20.3)
*cfclose
